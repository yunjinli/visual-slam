# Copyright 2020 Nikolaus Demmel <nikolaus@nikolaus-demmel.de>
# This file is distributed under the simplified BSD license:
#     https://opensource.org/licenses/BSD-2-Clause
#
# The other eigen config files are generated by cmake from an Eigen
# 3.3.7 setup. For the corresponding licenses see:
#     https://gitlab.com/libeigen/eigen/-/tree/3.3.7
# The changes are distributed under the same license as the original
# files.

# These files have been adapted from the generated config files from
# an Eigen 3.3.7 installation. The idea is that you can provide them
# alongside a source tree of Eigen, which can then be used with
# find_package() without installation.
#
# Either change the default path below, or pass EIGEN3_SOURCE_TREE to
# the cmake project, i.e.
#
#    cmake  -DEIGEN3_SOURCE_TREE=/path/to/source/tree/of/eigen
#
# To make sure cmake picks up these config files, pass the containing
# directory as CMAKE_PREFIX_PATH, i.e.
#
#    cmake  -DCMAKE_PREFIX_PATH=/path/to/cmake/eigen3
#

if(NOT EIGEN3_SOURCE_TREE)
  set(EIGEN3_SOURCE_TREE "${CMAKE_CURRENT_LIST_DIR}/../../thirdparty/eigen/")
endif()

# Extract Eigen version from Eigen/src/Core/util/Macros.h
set(EIGEN3_VERSION_FILE ${EIGEN3_SOURCE_TREE}/Eigen/src/Core/util/Macros.h)
if (NOT EXISTS ${EIGEN3_VERSION_FILE})
  eigen3_report_not_found(
    "Could not find file: ${EIGEN3_VERSION_FILE} "
    "containing version information in Eigen install located at: "
    "${EIGEN3_INCLUDE_DIR}.")
else ()
  file(READ ${EIGEN3_VERSION_FILE} EIGEN3_VERSION_FILE_CONTENTS)
  
  string(REGEX MATCH "#define EIGEN_WORLD_VERSION [0-9]+"
    EIGEN3_VERSION_MAJOR "${EIGEN3_VERSION_FILE_CONTENTS}")
  string(REGEX REPLACE "#define EIGEN_WORLD_VERSION ([0-9]+)" "\\1"
    EIGEN3_VERSION_MAJOR "${EIGEN3_VERSION_MAJOR}")
  
  string(REGEX MATCH "#define EIGEN_MAJOR_VERSION [0-9]+"
    EIGEN3_VERSION_MINOR "${EIGEN3_VERSION_FILE_CONTENTS}")
  string(REGEX REPLACE "#define EIGEN_MAJOR_VERSION ([0-9]+)" "\\1"
    EIGEN3_VERSION_MINOR "${EIGEN3_VERSION_MINOR}")
  
  string(REGEX MATCH "#define EIGEN_MINOR_VERSION [0-9]+"
    EIGEN3_VERSION_PATCH "${EIGEN3_VERSION_FILE_CONTENTS}")
  string(REGEX REPLACE "#define EIGEN_MINOR_VERSION ([0-9]+)" "\\1"
    EIGEN3_VERSION_PATCH "${EIGEN3_VERSION_PATCH}")
  
  # This is on a single line s/t CMake does not interpret it as a list of
  # elements and insert ';' separators which would result in 3.;2.;0 nonsense.
  set(EIGEN3_VERSION_STRING "${EIGEN3_VERSION_MAJOR}.${EIGEN3_VERSION_MINOR}.${EIGEN3_VERSION_PATCH}")
endif ()
